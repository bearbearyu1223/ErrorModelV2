--EMV2.aadl

property set EMV2
  is
  
OccurrenceDistribution : EMV2::DistributionSpecification
	  applies to ({emv2}**error propagation, {emv2}**error flow, {emv2}**error behavior event
	  ,{emv2}**error behavior state,{emv2}**error type,{emv2}**type set);
   

--needed to separate the property "Occurrence" from its type declaration
--so other properties may have that type
DistributionSpecification : type record (
	ProbabilityValue : aadlreal;
	OccurrenceRate : aadlreal;
	MeanValue : aadlreal;
	StandardDeviation : aadlreal;
	ShapeParameter : aadlreal;
	ScaleParameter : aadlreal;
	SuccessCount : aadlreal;
	SampleCount : aadlreal;
	Probability : aadlreal;
	Distribution : EMV2::DistributionFunction;);

--	Fixed represents a fixed distribution and takes a single parameter OccurrenceRate or ProbabilityValue. 
--	Poisson aka. Exponential represents an exponential distribution and takes a single parameter OccurrenceRate or ProbabilityValue.
--	Normal aka. Gauss represents a distribution with an explicitly specified MeanValue and StandardDeviation. 
--	Weibull represents a shaped distribution with a ShapeParameter and a ScaleParameter.
--	Binominal represents represents a discrete distribution with a SuccessCount, a SampleCount, and a Probability parameter.
	
DistributionFunction : type enumeration (Fixed, Poisson, Exponential, Normal, Gauss, Weibull, Binominal);
  
DurationDistributionSpecification : type record (
	Duration : Time_Range;
	Distribution : EMV2::DistributionFunction;); 
	  
DurationDistribution : EMV2::DurationDistributionSpecification applies to ({emv2}**Repair Event, {emv2}**Recover Event);

-- A PropagationTimeDelay property indicates the delay in propagating and error as a distribution over a time interval. 
-- For example, as a property associated with a connection it indicates the time delay of the error propagation.

PropagationTimeDelay: EMV2::DurationDistributionSpecification
    applies to (connection, {emv2}**propagation point connection);

StateKind : EMV2::StateKindEnum
	  applies to ({emv2}**error behavior state);

StateKindEnum: type enumeration (Working, NonWorking);

-- Indicates whether an error type is detectable
DetectionMechanism : aadlstring
	  applies to ({emv2}**error detection); 
 

-- The FaultKind property allows the user to specify whether an error source, 
-- i.e., the occurrence of a fault activation or a propagation is due to a design fault or an operational fault. 
-- Design faults are faults that could be eliminated at design time, but if present result in an error. 
-- Operational faults are faults that inherently occur during operation and should be detected and managed during operation.
FaultKind : EMV2::FaultKindEnum
	  applies to ( {emv2}**error event, {emv2}**error propagation, {emv2}**error source, {emv2}**error type, {emv2}**type set); 

FaultKindEnum: type enumeration (Design, Operational);


-- The Persistence property allows the user to specify whether an error is permanent, transient, or a singleton. 
-- A permanent error typically requires a repair action to the component with the fault occurrence. 
-- A transient error has a limited duration and typically requires a recovery action. 
-- In a discrete event system a transient error may last over several discrete events, e.g., a corrupted message may be sent over multiple periods. 
-- A singleton error occurs in the context of a single discrete event. For example, a divide by zero error may be specific to a computation on a particular input.
Persistence : EMV2::PersistenceEnum
	  applies to ({emv2}**error type, {emv2}**type set, {emv2}**error behavior state, 
	  {emv2}**error behavior event, {emv2}**error propagation);

PersistenceEnum: type enumeration (Permanent, Transient, Singleton);

-- The severity property value indicates the severity of the hazard ranging from 1 (high) to 5 (low). 
-- MIL-STD 882D suggests descriptive labels (Catastrophic, Critical, Marginal, Negligible) (See property set MILSTD882). 
-- ARP 4761 defines descriptive labels (Catastrophic, Hazardous, Major, Minor, NoEffect) (See property set ARP4761).
Severity : inherit aadlinteger applies to ({emv2}**error type, {emv2}**type set, {emv2}**error behavior state, 
	  {emv2}**error source, {emv2}**error propagation, {emv2}**error event);

-- The likelihood property value indicates the likelihood of the hazard ranging from A (high) to E (low). 
-- MIL-STD 882D suggests descriptive labels (Frequent, Probable, Occasional, Remote, Improbable) (See property set MILSTD882). 
-- ARP 4761 defines descriptive labels (Probable, Remote, ExtremelyRemote, ExtremelyImprobable) (See property set ARP4761).
Likelihood : inherit EMV2::LikelihoodLabels applies to ({emv2}**error type, {emv2}**type set, {emv2}**error behavior state, 
	  {emv2}**error source, {emv2}**error propagation, {emv2}**error event);

LikelihoodLabels: type enumeration (A,B,C,D,E);
DALLabels: type enumeration (A,B,C,D,E);

Hazards: list of record
  (
   crossreference     : aadlstring; -- cross reference to an external document
   hazardtitle		  : aadlstring; -- short descriptive phrase for hazard
   description        : aadlstring; -- description of the hazard (alternative to hazardtitle)
   failure            : aadlstring; -- description of system deviation, giving rise to failure effect
   failureeffect      : aadlstring; -- description of the consequences of a failure mode on system operation
   phases             : list of aadlstring; -- operational phases in which the hazard is relevant
   environment        : aadlstring; -- description of operational environment
   mishap             : aadlstring; -- description of event (series) resulting in unintentional death, etc.(MILSTD882)
   failurecondition   : aadlstring; -- description of event (series) resulting in unintentional death, etc.(ARP4761)
   risk               : aadlstring; -- description of risk. Risk is characterized by severity, likelihood, and occurrence probability
   severity           : aadlinteger; --
   likelihood         : EMV2::LikelihoodLabels;
   targetseverity     : aadlinteger; -- acceptable risk in terms of severity
   targetlikelihood   : EMV2::LikelihoodLabels; -- acceptable risk in terms of likelihood
   developmentassurancelevel : EMV2::DALLabels; -- level of rigor in development assurance
   verificationmethod : aadlstring; -- verification method to address the hazard
   safetyreport       : aadlstring; -- capturing the analysis/assessment of hazard resulting in value included in the hazard record.
   comment            : aadlstring; -- additional information about the hazard
   )
     applies to ({emv2}**error type, {emv2}**type set, {emv2}**error behavior state,
                 {emv2}**error source, {emv2}**error propagation, {emv2}**error event);

Description : aadlstring applies to (all); 


end EMV2;
